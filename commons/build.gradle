apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'androidx.navigation.safeargs.kotlin'

def getApiProperties() {
    def apiPropertiesFile = rootProject.file("api.properties")
    def apiProperties = new Properties()
    apiProperties.load(new FileInputStream(apiPropertiesFile))
    return apiProperties
}

android {
    compileSdkVersion rootProject.compileSdk
    buildToolsVersion rootProject.buildTools

    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    defaultConfig {
        minSdkVersion rootProject.minSdk
        targetSdkVersion rootProject.targetSdk
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        def apiProperties = getApiProperties()
        buildConfigField("String", "API_ENDPOINT", apiProperties["endpoint"])
        buildConfigField("String", "API_SPEAKERS_PATH", apiProperties["speakersPath"])
        buildConfigField("String", "API_SESSIONS_PATH", apiProperties["sessionsPath"])
        buildConfigField("String", "STATIC_MAPS_API_KEY", apiProperties["staticMapsApiKey"])

        buildConfigField("String[]", "CONFERENCE_DAYS_DATE", "{\"2019-12-20\", \"2019-12-21\"}")

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api 'androidx.core:core-ktx:1.1.0'

    // dagger 
    api "com.google.dagger:dagger-android:$rootProject.dagger"
    api "com.google.dagger:dagger-android-support:$rootProject.dagger"
    kapt "com.google.dagger:dagger-compiler:$rootProject.dagger"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.dagger"

    // navigation
    api "androidx.navigation:navigation-fragment-ktx:$rootProject.navigation"
    api "androidx.navigation:navigation-ui-ktx:$rootProject.navigation"

    // lifecycle
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.androidLifecycle"
    api "androidx.lifecycle:lifecycle-extensions:$rootProject.androidLifecycle"

    // network
    api 'com.squareup.retrofit2:retrofit:2.6.1'
    api 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.4.0'
    api 'com.squareup.okhttp3:logging-interceptor:4.2.1'

    // room
    api "androidx.room:room-runtime:$rootProject.roomDatabase"
    api "androidx.room:room-ktx:$rootProject.roomDatabase"
    kapt "androidx.room:room-compiler:$rootProject.roomDatabase"

    // arrow core
    api "io.arrow-kt:arrow-core:$rootProject.arrow"
    api "io.arrow-kt:arrow-syntax:$rootProject.arrow"
    kapt "io.arrow-kt:arrow-meta:$rootProject.arrow"

    // coroutines
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2'

    // moshi
    implementation("com.squareup.moshi:moshi-kotlin:1.9.1")

    // okhttp
    implementation("com.squareup.okhttp3:okhttp:4.2.2")

    // time APIs
    api 'com.jakewharton.threetenabp:threetenabp:1.2.1'

    //glide
    api 'com.github.bumptech.glide:glide:4.10.0'
    kapt 'com.github.bumptech.glide:compiler:4.10.0'

    // tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.2'
    testImplementation("com.squareup.okhttp3:mockwebserver:4.2.2")

    androidTestImplementation "android.arch.core:core-testing:1.1.1"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.2"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2"
    androidTestImplementation "io.mockk:mockk-android:1.9.3.kotlin12"
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation "androidx.room:room-testing:$rootProject.roomDatabase"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
}
